" vim: set foldmethod=marker foldlevel=0 nomodeline:

"         ██╗   ██╗ ██╗ ███╗   ███╗ ██████╗   ██████╗
"         ██║   ██║ ██║ ████╗ ████║ ██╔══██╗ ██╔════╝
"         ██║   ██║ ██║ ██╔████╔██║ ██████╔╝ ██║
"         ╚██╗ ██╔╝ ██║ ██║╚██╔╝██║ ██╔══██╗ ██║
"       ██╗╚████╔╝  ██║ ██║ ╚═╝ ██║ ██║  ██║ ╚██████╗
"       ╚═╝ ╚═══╝   ╚═╝ ╚═╝     ╚═╝ ╚═╝  ╚═╝  ╚═════╝

" ==============================================================================
"   Settings {{{
" ==============================================================================

set autoindent                       " Enable automatic indentation.
set background=dark                  " Enable colors for dark backgrounds.
set backspace=indent,eol,start       " Backspace works on everything.
set colorcolumn=80                   " Colored column to see textwidth.
set completeopt=menuone
set concealcursor=nc                 " Conceal in normal & command mode.
set conceallevel=1                   " Concealed text replaced by one character.
set cursorline                       " Highlight the line with the cursor.
set expandtab                        " Convert tabs to spaces.
set encoding=utf-8                   " Set encoding to UTF-8.
set hidden                           " Edit multiple unsaved buffers.
set history=1000                     " Remember the last thousand commands.
set hlsearch                         " Highlight search terms.
set ignorecase                       " Ignore case while searching.
set incsearch                        " Highlight matches as you type.
set laststatus=2                     " Always show the status line.
set lazyredraw                       " Fixes slow scrolling with cursorline.
set linebreak                        " Break on words not end of space.
set listchars=                       " Set default whitespace characters.
set listchars+=trail:·
set listchars+=precedes:«
set listchars+=extends:»
set listchars+=eol:⏎
set listchars+=tab:▸-
set modeline                         " Check files for modelines.
set mouse=a                          " Enable mouse support.
set mousehide                        " Hide the mouse cursor when typing.
set noshowmode                       " Don't show the current mode.
set nostartofline                    " Go to previous cursor location when
                                     " switching between buffers.
set nowrap                           " Don't wrap long lines.
set number                           " Always show line numbers.
set omnifunc=syntaxcomplete#Complete " Syntax completion with <c-x><c-o>.
set ruler                            " Enable the ruler.
set scrolloff=8                      " Keep 8 lines above/below the cursor.
set sidescrolloff=10                 " Keep 10 columns next to the cursor.
set shiftwidth=4                     " Use four spaces when auto-indenting.
set showcmd                          " Show partial command in status line.
set showmatch                        " Briefly jump to matching brackets.
set smartcase                        " Override the ignorecase option if
                                     " the search has upper case characters.
set smartindent                      " Smart autoindenting for new lines.
set smarttab                         " Use shiftwidth when using <tab> in
                                     " front of a line.
set softtabstop=4                    " How many spaces should be inserted
                                     " when hitting <tab> or backspace.
set splitbelow                       " Place new splits below and
set splitright                       " to the right.
set tabstop=4                        " Set <tab> to display four spaces.
set textwidth=80                     " Maximum text length before a new line.
set timeoutlen=500                   " Key time out length.
set ttyfast                          " Indicates a fast terminal connection.
set undodir=~/.vim/undo//            " Save undo history to this location.
set undofile                         " Enable persistent undos.
set undolevels=9999                  " Number of changes that can be undone.
set undoreload=9999                  " Number of lines to save on buffer reload.
set updatetime=250                   " Frequency of updating.
set viminfo='20,\"50                 " Retain various info between sessions.
set virtualedit=block                " Visual block mode can select blank space.
set whichwrap=b,s,<,>,[,]            " Allow keys to move the cursor to the
            " | | | | | |              previous/next line.
            " | | | | | |
            " | | | | | +------- ] Insert and Replace
            " | | | | +--------- [ Insert and Replace
            " | | | +----------- <right> Normal and Visual
            " | | +------------- <left> Normal and Visual
            " | +--------------- <space> Normal and Visual
            " +----------------- <bs> Normal and Visual
set wildignore+=*.swp,*.o,*.mod
set wildmenu                         " Autocomplete commands.
set wildmode=longest:full,full       " Autocomplete til the longest common
                                     " string and the next full match.

" }}}
" ==============================================================================
"   Leader {{{
" ==============================================================================

let mapleader = "\<space>"
let maplocalleader = "\<space>"

" }}}
" ==============================================================================
"   Plugins {{{
" ==============================================================================

"
"---Specify a directory for plugins.
"   Keep Plug commands between plug#begin/end.
"
if has('nvim')
  call plug#begin('~/.local/share/nvim/plugged')
else
  call plug#begin('~/.vim/plugged')
end

Plug 'airblade/vim-gitgutter'          " --- Git gutter.
Plug 'chrisbra/vim-diff-enhanced'      " --- Enhanced vimdiff.
Plug 'easymotion/vim-easymotion'       " --- Easy motions.
Plug 'justinmk/vim-sneak'              " --- Extra motions.
Plug 'kana/vim-textobj-line'           " --- Text objects for current line.
Plug 'kana/vim-textobj-user'           " --- Dependency for vim-textobj-line.
Plug 'michaeljsmith/vim-indent-object' " --- Indent text object.
Plug 'tpope/vim-commentary'            " --- Comment stuff out.
Plug 'tpope/vim-eunuch'                " --- Unix commands.
Plug 'tpope/vim-fugitive'              " --- Git integration.
Plug 'tpope/vim-repeat'                " --- Repeating plugin maps.
Plug 'tpope/vim-rsi'                   " --- Readline features in command line.
Plug 'tpope/vim-surround'              " --- Quoting/parenthesizing made simple.
Plug 'tpope/vim-unimpaired'            " --- Bracket mappings.
Plug 'unblevable/quick-scope'          " --- Hightlight f, F, t, T.

"
"---Initialize plugin system.
"
call plug#end()

" }}}
" ==============================================================================
"   Plugin options {{{
" ==============================================================================

"
"---easymotion/vim-easymotion
"
let g:EasyMotion_smartcase = 1

"
"---justinmk/vim-sneak
"
let g:sneak#use_ic_scs = 1

"
"---unblevable/quick-scope
"
let g:qs_highlight_on_keys = ['f', 'F', 't', 'T']

" }}}
" ==============================================================================
"   Key mappings {{{
" ==============================================================================

"
"---Jump down by half a screen.
"
noremap <c-f> <c-d>
noremap <c-b> <c-u>

"
"---Paste last yank, make Y consistent with C and D.
"
nnoremap Y y$

"
"---Allow . command to work in visual mode.
"
vnoremap . :norm.<cr>

"
"---Move between editor lines rather than actual lines.
"
vnoremap j gj
vnoremap k gk
vnoremap $ g$
vnoremap ^ g^
vnoremap 0 g0
nnoremap j gj
nnoremap k gk
nnoremap $ g$
nnoremap ^ g^
nnoremap 0 g0

"
"---Reverse mappings for above.
"
vnoremap gj j
vnoremap gk k
vnoremap g$ $
vnoremap g^ ^
vnoremap g0 0
nnoremap gj j
nnoremap gk k
nnoremap g$ $
nnoremap g^ ^
nnoremap g0 0

"
"---Exit to normal mode.
"
inoremap fd <esc>
vnoremap fd <esc>

"
"---Unmap F1 as help.
"
inoremap <f1> <esc>
nnoremap <f1> <esc>
vnoremap <f1> <esc>

"
"---Stop cursor jumping when joining lines.
"
nnoremap J mzJ`z

"
"---Split line (sister to [J]oin lines).
"
nnoremap S i<cr><esc>^mwgk:silent! s/\v +$//<cr>:noh<cr>`w

"
"---Search a visual selection.
"
vnoremap / <esc>/\%><c-r>=line("'<")-1<cr>l\%<<c-r>=line("'>")+1<cr>l
vnoremap ? <esc>?\%><c-r>=line("'<")-1<cr>l\%<<c-r>=line("'>")+1<cr>l

"
"---Moving lines.
"
nnoremap <silent> <c-k> :move-2<cr>
nnoremap <silent> <c-j> :move+<cr>
nnoremap <silent> <c-h> <<
nnoremap <silent> <c-l> >>
xnoremap <silent> <c-k> :move-2<cr>gv
xnoremap <silent> <c-j> :move'>+<cr>gv
xnoremap <silent> <c-h> <gv
xnoremap <silent> <c-l> >gv
xnoremap < <gv
xnoremap > >gv

"
"---Move to start and end of line.
"
noremap H ^
noremap L $
vnoremap L g_

"
"---Move to last change (like gi).
"
nnoremap gI `.

"
"---Keep flags when repeating last substitute command.
"
nnoremap & :&&<cr>
xnoremap & :&&<cr>

" }}}
" ==============================================================================
"   Miscellaneous {{{
" ==============================================================================

"
"---Clipboard -- use system for copy/paste.
"
let os = substitute(system('uname'), "\n", "", "")
let hostname = substitute(system('hostname'), "\n", "", "")
if os ==? "darwin"
    set clipboard=unnamedplus
elseif os ==? "linux"
    set clipboard=unnamedplus
    autocmd VimLeave * call system("xsel -ib", getreg('+'))
    nnoremap <silent> <c-z> :call system("xsel -ib", getreg('+'))<cr><c-z>
endif

"
"---Grep.
"
" Bind K to grep word under cursor.
nnoremap K :grep! "\b<c-r><c-w>\b"<cr>:cw<cr>
" Push quickfix window always to the bottom
autocmd FileType qf wincmd J

" }}}
" ==============================================================================
"   Postscript {{{
" ==============================================================================

"
"---Format options: always last, but before filetypes, to override ftplugins.
"
augroup postscript
    autocmd!
    autocmd FileType * setlocal formatoptions=
    autocmd FileType * setlocal formatoptions+=tcrjq formatoptions-=o
    "                                          |||||                |
    " Auto-wrap text using textwidth ----------+||||                |
    " inserting the current comment leader -----+|||                |
    " Auto insert the current comment leader     |||                |
    " after hitting <enter> in Insert mode. -----+||                |
    " Remove comment lead on join. ---------------+|                |
    " Allow formatting comments with 'gq' ---------+                |
    " Do not continue comment using o or O -------------------------+
    " automatically.
augroup END

" }}}
" ==============================================================================
"   Filetypes {{{
" ==============================================================================

"---Fortran.
augroup fortran
    autocmd!
    autocmd FileType fortran setlocal shiftwidth=2 tabstop=2 textwidth=80
    autocmd FileType fortran let fortran_more_precise=1
    autocmd FileType fortran let fortran_do_enddo=1
    autocmd FileType fortran let fortran_indent_more=0
augroup END

"---HTML.
augroup html
    autocmd!
    autocmd FileType html setlocal shiftwidth=2 tabstop=2
augroup END

"---Markdown.
augroup markdown
    autocmd!
    autocmd FileType markdown setlocal spell
    autocmd FileType markdown setlocal formatoptions-=t
    autocmd FileType markdown setlocal wrap
augroup END

"---Python.
augroup python
    autocmd!
    autocmd FileType python setlocal shiftwidth=4 tabstop=4
    autocmd FileType python setlocal textwidth=88 colorcolumn=88
augroup END

"---reStructuredText.
augroup rst
    autocmd!
    autocmd FileType rst setlocal spell
    autocmd FileType rst setlocal formatoptions-=t
    autocmd FileType rst setlocal wrap
augroup END

"---Shell.
augroup shell
    autocmd!
    autocmd FileType sh,zsh setlocal shiftwidth=2 tabstop=2
augroup END

"---Tex.
augroup tex
    autocmd!
    autocmd FileType tex setlocal spell shiftwidth=3 tabstop=3
augroup END

"---YAML.
augroup yaml
    autocmd!
    autocmd FileType yaml setlocal shiftwidth=2 tabstop=2
augroup END

" }}}
